plugins {
    id "com.jfrog.bintray" version "1.6"
}

allprojects { 
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'war'
	apply plugin: 'eclipse'
	apply plugin: 'eclipse-wtp'
	apply plugin: 'jetty'
}

description = """Jenkins Test Tracker Web Services"""
group = 'JttWs'

def buildNumberFromEnvironment = System.getenv()[ 'VERSION_NUMBER' ]
def buildNumberForThisEnvironment = ( buildNumberFromEnvironment != null ? buildNumberFromEnvironment : 'WORKSPACE' ) 
version = buildNumberForThisEnvironment

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url  "http://dl.bintray.com/dangrew/SystemDigest" }
    maven { url  "http://dl.bintray.com/dangrew/JsonUpgradingPersistenceArchitecture" }
    maven { url  "http://dl.bintray.com/dangrew/JttModel" }
    maven { url  "http://dl.bintray.com/dangrew/JttConnection" }
}

dependencies {
	compile 'ch.qos.logback:logback-classic:1.1.3'
	compile 'org.springframework:spring-webmvc:4.3.6.RELEASE'
	compile 'javax.servlet:jstl:1.2'
	compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
	compile group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.1.2'
	compile group: 'SystemDigest', name: 'SystemDigest', version: '1.2.52'
    compile group: 'jupa', name: 'JsonUpgradingPersistenceArchitecture', version: '1.1.55'
    compile group: 'jtt', name: 'jtt-model', version:'1.0.2'
	compile group: 'jtt', name: 'jtt-connection', version:'1.0.7'
	
	testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version:'1.0.5'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.3'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
	testCompile group: 'org.springframework', name: 'spring-test', version: '4.3.6.RELEASE'
	testCompile group: 'SystemDigest', name: 'SystemDigest', version: '1.2.52', classifier: 'tests'
	testCompile group: 'jtt', name: 'jtt-model', version:'1.0.2', classifier: 'tests'
	testCompile group: 'jtt', name: 'jtt-connection', version:'1.0.7', classifier: 'tests'
}

// Embeded Jetty for testing
jettyRun{
	contextPath = "jttws"
	httpPort = 8080
}

jettyRunWar{
	contextPath = "jttws"
	httpPort = 8080
}

//For Eclipse IDE only
eclipse {
  wtp {
    component {
      //define context path, default to project folder name
      contextPath = 'jttws'
    }
  }
  classpath {
     downloadSources=true
  }
}

/*
 * UNIT TESTS: configure reports and scope of tests
 */
test {
    filter {
//test intermingled unit + integ - arguably against conventions
        includeTestsMatching "*Test"
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
    reports {
	    junitXml.enabled = true
	    html.enabled = false
    }
}

task packageTests(type: Jar) {
  from sourceSets.test.output
  classifier = 'tests'
}
artifacts.archives packageTests

bintray {
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']
    publications = ['core']
    
    publish = true //auto publish everything
    
    pkg {
        repo = 'JttWs'
        name = 'JttWs'
        userOrg = bintrayUser
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/DanGrew/JttWs.git'
        
        version {
            name = buildNumberForThisEnvironment
            desc = 'Continuous Delivery rolling version'
            released = new Date()
        }
    }
}

publishing {
    publications {
    	//publish standard core components - jar + tests
        core(MavenPublication) {
        	//publish all items
            from components.java
            
        	//configure description
            groupId 'JttWs'
            artifactId 'JttWs'
            version version
        }
    }
}

